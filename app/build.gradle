repositories {
    maven {
        url 'https://raw.githubusercontent.com/felixb/mvn-repo/master/'
    }
}

apply plugin: 'com.android.application'

File releaseProps = new File("release.properties")
String gitSha = 'git rev-parse --verify --short HEAD'.execute().text.trim()

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "de.ub0r.android.choosebrowser"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 230012000
        versionName "1.2"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        setProperty("archivesBaseName", "choose-browser-$gitSha")
    }
    signingConfigs {
        release {
            if (releaseProps.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(releaseProps))

                storeFile file("../release.ks")
                storePassword props.getProperty("releaseKeystorePassword")
                keyAlias "jarsign"
                keyPassword props.getProperty("releaseKeyPassword")
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }

        release {
            if (releaseProps.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'de.ub0r.android.logg0r:logg0r:2.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
}
